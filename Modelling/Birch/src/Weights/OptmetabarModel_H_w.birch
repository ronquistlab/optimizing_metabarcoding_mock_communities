/**
 * Model for optimal metabarcoding study,  Homogenate data set
 * Mean weight of the 4 spikeins: 1.1225
 */
class OptmetabarModel_H_w < Model {
  k:Real;
  θ:Real;
  μ:Random<Real>;
  σ2:Random<Real>;
  c:Real[21];
  Reads:Integer[_];

  override function simulate() {
    k <~ Gamma(1.0, 10.0);
    let logθ <~ Gaussian(0.0, pow(2.0, 2));
    θ <- exp(logθ);
    σ2 ~ InverseGamma(3.0, 3.0*9.0);
    μ ~ Gaussian(6.5, σ2);
  }

  override function simulate(j:Integer) {
       let logC <~ Gaussian(μ, σ2);
       c[j] <- exp(logC);
          for t in 1..4 {
            if Reads[t] > 0 {
              Reads[t] ~> Gamma(k*1.1225, c[j]*θ);
            }
          }
   }

  override function read(j:Integer, buffer:Buffer) {
    Reads <-? buffer.get<Integer[_]>();
  }

  override function write(buffer:Buffer) {
    buffer.set("k", k);
    buffer.set("θ", θ);
    for i in 1..21 { buffer.set("c" + i, c[i]); }
    buffer.set("μ", μ);
    buffer.set("σ2", σ2);
  }
}

